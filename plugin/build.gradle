plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '2.0.0'
}

group = 'org.fbluemle'
version = '0.4.0'
base {
    archivesName = 'adbstart'
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifactId = 'adbstart'
        }
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    compileOnly 'com.android.tools.build:gradle:8.6.0'
    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4.13.2'
}

gradlePlugin {
    website = 'https://github.com/fbluemle/adbstart-plugin'
    vcsUrl = 'https://github.com/fbluemle/adbstart-plugin'
    plugins {
        adbStart {
            id = 'org.fbluemle.adbstart'
            implementationClass = 'org.fbluemle.adbstart.AdbStartPlugin'
            displayName = 'ADB Start for Android/RN'
            description = 'Adds start<Variant> tasks that install & launch the correct Android applicationId via adb.'
            tags.set(['android', 'react-native', 'adb', 'variants'])
        }
    }
}

sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)
configurations.functionalTestRuntimeOnly.extendsFrom(configurations.testRuntimeOnly)

tasks.register('functionalTest', Test) {
    description = 'Runs functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    shouldRunAfter test
}

gradlePlugin.testSourceSets.add(sourceSets.functionalTest)

tasks.named('check') {
    dependsOn(tasks.functionalTest)
}
